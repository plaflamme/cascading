/*
 * Copyright (c) 2007-2012 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: '../testing.gradle'

mavenPomDir = "${buildDir}/poms/hadoop"
archivesBaseName = 'cascading-hadoop'

sourceSets {
  main {
    output.classesDir = new File( "${buildDir}/classes/hadoop" )

    java {
      srcDirs projectDir
      exclude 'build.gradle'
    }

    resources {
      srcDirs projectDir
      exclude 'build.gradle'
    }
  }
}

dependencies {
  compile( group: 'org.apache.hadoop', name: 'hadoop-core', version: hadoopVersion ) {
    exclude group: 'ant'
    exclude group: 'junit'
  }
  compile( group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopHdfsVersion ) {
    exclude group: 'ant'
    exclude group: 'junit'
    // commons-daemon-1.0.3 pom file is broken
    exclude group: 'commons-daemon'
  }
  compile( group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopHdfsVersion ) {
    exclude group: 'ant'
    exclude group: 'junit'
    // commons-daemon-1.0.3 pom file is broken
    exclude group: 'commons-daemon'
  }
}

assemble << {
  def distDir = mkdir( "${buildDir}/dist" )

  copy {
    into "${distDir}/lib/cascading-hadoop"
    from( configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.findAll { dep ->
      !dep.name.contains( 'cascading' ) && !dep.name.contains( 'hadoop' )
    }.collect { dep ->
      dep.moduleArtifacts.collect { it.file }
    } )
  }

  copy {
    into distDir
    from configurations.default.allArtifactFiles
  }
}

test {
  include '**/*PlatformTest.class'
  include '**/*HadoopTest.class'

  systemProperties = setTestingProperties( 'hadoop' )

  testReportDir = new File( testReportDir, 'hadoop' )
  testResultsDir = new File( testResultsDir, 'hadoop' )
}
