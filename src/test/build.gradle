/*
 * Copyright (c) 2007-2012 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: '../testing.gradle'

mavenPomDir = "${buildDir}/poms/test"
archivesBaseName = 'cascading-test'

sourceSets {
  main {
    output.classesDir = new File( "${buildDir}/classes/test" )

    java {
      srcDirs projectDir
      exclude 'build.gradle'
    }

    resources {
      srcDirs projectDir
      exclude 'build.gradle'
    }
  }
}

dependencies {
  compile group: 'junit', name: 'junit', version: '4.8.+'
  compile group: 'org.apache.hadoop', name: 'hadoop-test', version: hadoopVersion
  compile( group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: hadoopHdfsVersion)  {
    // commons-daemon-1.0.3 pom file is broken
    exclude group: 'commons-daemon'
  }
  compile( group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopHdfsVersion ) {
    exclude group: 'ant'
    exclude group: 'junit'
    // commons-daemon-1.0.3 pom file is broken
    exclude group: 'commons-daemon'
  }

}

if( hadoopVersion.startsWith( "1.0." ) )
{
  dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.1'

    runtime 'javax.ws.rs:jsr311-api:1.1.1' // missed dep in hadoop, go figure
  }
}

if( hadoopVersion.startsWith( "0.20." ) )
{
  dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.0.1'
  }
}

if( hadoopVersion.startsWith( "0.20.205." ) )
{
  dependencies {
    runtime 'javax.ws.rs:jsr311-api:1.1.1' // missed dep in hadoop, go figure
    runtime 'org.codehaus.jackson:jackson-core-asl:1.0.1'
    runtime 'org.codehaus.jackson:jackson-mapper-asl:1.0.1'
  }
}

assemble << {
  def distDir = mkdir( "${buildDir}/dist" )

  copy {
    into "${distDir}/lib/cascading-test"
    from( configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.findAll { dep ->
      !dep.name.contains( 'cascading' ) && !dep.name.contains( 'hadoop' )
    }.collect { dep ->
      dep.moduleArtifacts.collect { it.file }
    } )
  }

  copy {
    into distDir
    from configurations.default.allArtifactFiles
  }
}

test {
  include '**/ComparePlatformsTest.class'

  systemProperties = setTestingProperties( 'compare' )

  testReportDir = new File( testReportDir, 'compare' )
  testResultsDir = new File( testResultsDir, 'compare' )
}
